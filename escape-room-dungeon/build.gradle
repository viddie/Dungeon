plugins {
    id 'java-library'
}


dependencies {
    api project(':dungeon')
}


sourceSets.main.java.srcDirs = ['src/']
sourceSets.main.resources.srcDirs = ['assets/']

tasks.register('runEscapeRoom', JavaExec) {
    mainClass = 'starter.EscapeRoomDungeon'
    classpath = sourceSets.main.runtimeClasspath
}
tasks.register('buildEscapeRoomJar', Jar) {
    dependsOn ':game:jar', ':dungeon:jar'
    archiveBaseName = 'EscapeRoom'
    from sourceSets.main.output
    from project(':game').sourceSets.main.output
    from project(':dungeon').sourceSets.main.output
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'starter.EscapeRoomDungeon'
    }

    archiveFileName = 'EscapeRoom.jar'

    into('assets') {
        from new File(project(':game').projectDir, '/assets')
        from new File(project(':dungeon').projectDir, '/assets')
    }

    exclude('META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
}

processResources {
    from new File(project(':game').projectDir,    '/assets')
    from new File(project(':dungeon').projectDir, '/assets')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
